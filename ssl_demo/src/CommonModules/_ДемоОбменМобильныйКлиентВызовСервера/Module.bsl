#Область СлужебныеПроцедурыИФункции

// Запускает синхронизацию основного и автономного серверов.
// 
// Параметры:
//  КодУзла - Строка
//  НаименованиеМобильногоКомпьютера - Строка
//  НомерОтправленного - Число
//  НомерПринятого - Число
//
// Возвращаемое значение:
//  Строка - код узла синхронизации
//
Функция НачатьСинхронизацию(КодУзла, НаименованиеМобильногоКомпьютера, НомерОтправленного, НомерПринятого) Экспорт
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.ПланыОбмена._ДемоМобильныйКлиент) Тогда
		ВызватьИсключение(НСтр("ru='Недостаточно прав на синхронизацию данных с мобильным приложением.'"), 
			КатегорияОшибки.НарушениеПравДоступа);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(Метаданные.ПланыОбмена._ДемоМобильныйКлиент.ПолноеИмя());
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ПланыОбмена._ДемоМобильныйКлиент.ЭтотУзел());
		Блокировка.Заблокировать();

		УзелОбмена = ПланыОбмена._ДемоМобильныйКлиент.ЭтотУзел().ПолучитьОбъект();
		Если НЕ ЗначениеЗаполнено(УзелОбмена.Код) Тогда
			УзелОбмена.Код = "001";
			УзелОбмена.Наименование = НСтр("ru = 'Центральный'");
			УзелОбмена.Записать();
		КонецЕсли;

		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	УзелОбмена = ПланыОбмена._ДемоМобильныйКлиент.НайтиПоКоду(КодУзла); 
	Если УзелОбмена.Пустая() Тогда
		
		НовыйУзел = ПланыОбмена._ДемоМобильныйКлиент.СоздатьУзел();
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			Блокировка.Добавить("Константа._ДемоКодНовогоУзлаПланаОбмена");
			Блокировка.Заблокировать();
			
			КодНовогоУзла = Константы._ДемоКодНовогоУзлаПланаОбмена.Получить();
			Если КодНовогоУзла = 0 Тогда 
				КодНовогоУзла = 2;
			КонецЕсли;	
			Константы._ДемоКодНовогоУзлаПланаОбмена.Установить(КодНовогоУзла + 1);
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
		
		Если СтрДлина(КодНовогоУзла) < 3 Тогда
			НовыйУзел.Код = Формат(КодНовогоУзла, "ЧЦ=3; ЧВН=");
		Иначе
			НовыйУзел.Код = КодНовогоУзла;
		КонецЕсли;
		НовыйУзел.Наименование = НаименованиеМобильногоКомпьютера;
		НовыйУзел.НомерОтправленного = НомерОтправленного;
		НовыйУзел.НомерПринятого = НомерПринятого;
		НовыйУзел.Записать();
		
		_ДемоОбменМобильныйКлиент.ЗарегистрироватьИзмененияДанных(НовыйУзел.Ссылка);
		Возврат НовыйУзел.Код;
		
	КонецЕсли;
		
	ЗарегистрироватьИзмененияДанных = Ложь;
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(Метаданные.ПланыОбмена._ДемоМобильныйКлиент.ПолноеИмя());
		ЭлементБлокировки.УстановитьЗначение("Ссылка", УзелОбмена);
		Блокировка.Заблокировать();
		
		СведенияОбУзлеОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УзелОбмена, 
			"Наименование,ПометкаУдаления,НомерОтправленного,НомерПринятого");
		Если СведенияОбУзлеОбмена.ПометкаУдаления 
			Или СведенияОбУзлеОбмена.Наименование <> НаименованиеМобильногоКомпьютера Тогда
			
			Узел = УзелОбмена.ПолучитьОбъект();
			Узел.ПометкаУдаления = Ложь;
			Узел.Наименование = НаименованиеМобильногоКомпьютера;
			Узел.Записать();
			
		КонецЕсли;
		
		ЗарегистрироватьИзмененияДанных = СведенияОбУзлеОбмена.НомерОтправленного <> НомерОтправленного 
			Или СведенияОбУзлеОбмена.НомерПринятого <> НомерПринятого;
		Если ЗарегистрироватьИзмененияДанных Тогда
			
			Узел = УзелОбмена.ПолучитьОбъект();
			Узел.НомерОтправленного = НомерОтправленного;
			Узел.НомерПринятого = НомерПринятого;
			Узел.Записать();
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Если ЗарегистрироватьИзмененияДанных Тогда
		_ДемоОбменМобильныйКлиент.ЗарегистрироватьИзмененияДанных(УзелОбмена);
	КонецЕсли;
		
	Возврат Узел.Код;
	
КонецФункции

// Выполняет синхронизацию данных:
// получает пакет изменений предназначенных для данного узла и
// записывает пакет изменений принятых от данного узла.
//
// Параметры:
//  КодУзла - Строка - код узла, с которым идет синхронизация данных.
//  ДанныеМобильногоПриложения - ХранилищеЗначения - хранилище, в которое помещен пакет обмена.
//
// Возвращаемое значение:
//  ХранилищеЗначения
//
Функция ВыполнитьОбменДанными(КодУзла, ДанныеМобильногоПриложения) Экспорт
	
	УзелОбмена = ПланыОбмена._ДемоМобильныйКлиент.НайтиПоКоду(КодУзла); 
	
	Если УзелОбмена.Пустая() Тогда
		ТекстОшибки = НСтр("ru='Невозможно выполнить синхронизацию данных с мобильным устройством, т.к. соответствующий ему узел с кодом %1 не существует.'");
		ВызватьИсключение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, КодУзла));
	КонецЕсли;
	
	_ДемоОбменМобильныйКлиент.ПринятьПакетОбмена(УзелОбмена, ДанныеМобильногоПриложения);
	Возврат _ДемоОбменМобильныйКлиент.СформироватьПакетОбмена(УзелОбмена);
	
КонецФункции

// Проверяет наличие прав на запуск синхронизации основного и автономного серверов.
//
// Возвращаемое значение:
//  Булево - Истина, если есть права на запуск синхронизации.
//
Функция ТребуетсяОбменДаннымиСАвтономнымПриложением() Экспорт

	// АПК:336-выкл Не заменять на РолиДоступны, т.к. проверка ролей осуществляется в режиме автономной конфигурации.
	// @skip-check using-isinrole
	Возврат РольДоступна("_ДемоОбменМобильныйКлиент");
	// АПК:336-вкл
	
КонецФункции

#КонецОбласти